plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = 'net.momirealms'
version = '3.1.3'

repositories {
    mavenCentral()
    maven {name = "aliyun repo"; url = "https://maven.aliyun.com/repository/public/"}
    maven {name = 'papermc repo'; url = 'https://papermc.io/repo/repository/maven-public/'}
    maven {name = 'sonatype'; url = 'https://oss.sonatype.org/content/groups/public/'}
    maven {name = "dmulloy2 repo"; url = "https://repo.dmulloy2.net/repository/public/"}
    maven {name = "clip repo"; url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'}
    maven {name = "codemc repo"; url = "https://repo.codemc.org/repository/maven-public/"}
    maven {name = "enginehub repo"; url = "https://maven.enginehub.org/repo/"}
    maven {name = "Lumine repo"; url = "https://mvn.lumine.io/repository/maven-public"}
    maven {name = "jitpack repo"; url = "https://jitpack.io"}
    maven {name = 'glaremasters repo'; url = 'https://repo.glaremasters.me/repository/towny/'}
    maven {name = 'bg-software repo'; url = 'https://repo.bg-software.com/repository/api/'}
}

dependencies {
    compileOnly ('dev.dejvokep:boosted-yaml:1.3')
    compileOnly ('com.github.Archy-X:AureliumSkills:Beta1.3.18')
    compileOnly ('commons-io:commons-io:2.11.0')
    compileOnly ('dev.folia:folia-api:1.19.4-R0.1-SNAPSHOT')
    compileOnly ('io.papermc.paper:paper-api:1.19.4-R0.1-SNAPSHOT')
    compileOnly ('me.clip:placeholderapi:2.11.3')
    compileOnly ('com.github.LoneDev6:api-itemsadder:3.4.1-r4')
    compileOnly ('io.lumine:Mythic-Dist:5.0.3-SNAPSHOT')
    compileOnly ('com.willfp:EcoSkills:2.2.6')
    compileOnly ('com.willfp:eco:6.53.1')
    compileOnly ('com.willfp:EcoJobs:3.2.5')
    compileOnly ('net.objecthunter:exp4j:0.4.8')
    compileOnly ('io.lumine:Mythic-Dist:5.2.1')
    compileOnly fileTree (dir:'libs',includes:['*.jar'])
    implementation ('net.kyori:adventure-api:4.13.1')
    implementation ('net.kyori:adventure-platform-bukkit:4.3.0')
    implementation ('net.kyori:adventure-text-minimessage:4.13.1')
    implementation ("de.tr7zw:item-nbt-api:2.11.2")
    implementation ('org.bstats:bstats-bukkit:3.0.1')
    implementation fileTree (dir:'libs',includes:['BiomeAPI.jar','ProtectionLib.jar'])
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching(['paper-plugin.yml', 'plugin.yml']) {
        expand props
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.release = targetJavaVersion
}

shadowJar {
    relocate ('de.tr7zw', 'net.momirealms.customcrops.libs.de.tr7zw')
    relocate ('net.kyori', 'net.momirealms.customcrops.libs.net.kyori')
    relocate ('org.bstats', 'net.momirealms.customcrops.libs.org.bstats')
    relocate ('org.apache.commons.pool2', 'net.momirealms.customcrops.libs.org.apache.commons.pool2')
    relocate ('net.momirealms.biomeapi', 'net.momirealms.customcrops.libs.net.momirealms.biomeapi')
    relocate ('net.momirealms.protectionlib', 'net.momirealms.customcrops.libs.net.momirealms.protectionlib')
}

tasks.register("delete", Delete).get().delete("build/libs/"+project.name+"-"+project.version+".jar")
tasks.named("build").get().dependsOn("shadowJar").finalizedBy("delete").doLast {
    println("Deleting: "+ "build/libs/"+project.name+"-"+project.version+".jar")
}